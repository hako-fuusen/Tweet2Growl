apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'java-library-distribution'

sourceCompatibility = 1.8
version = '1.0'

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

// java-library-distributionプラグインが必要とする
ext.mainClassName = 't2g.Main'

// 依存性の管理
repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
    compile 'org.twitter4j:twitter4j-core:4.0.2'
    compile 'org.twitter4j:twitter4j-stream:4.0.2'
    compile 'com.google.code.jgntp:jgntp:1.2.1'
}

// jar作成の際のプロパティ
jar {
	// 出力するJARファイル名を固定
    archiveName = "${baseName}.${extension}"
    
    // mavenのセントラルリポジトリから取得したjarファイルをクラスパスに自動的に追加する
    // 依存性情報が事前に定義されていないと情報が取得できない。dependenciesより下で定義すること。
    manifest {
        attributes ( 
        	// java-library-distribution.distZipで出力する都合上ライブラリが別フォルダになるので、その補正を服務
        	'Class-Path': configurations.runtime.files.collect { 'lib\\' + it.name }.join(' ') ,
        	'Main-Class': mainClassName
        )
    }
}

test {
    systemProperties 'property': 'value'
}
